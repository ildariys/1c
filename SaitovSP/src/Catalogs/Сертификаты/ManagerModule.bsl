
#Область ПрограммныйИнтерфейс
	
Процедура Печать(ТабДок, Ссылка) Экспорт
	Макет = Справочники.Сертификаты.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сертификаты.ДатаВыдачи,
	|	Сертификаты.Код,
	|	Сертификаты.Сумма
	|ИЗ
	|	Справочник.Сертификаты КАК Сертификаты
	|ГДЕ
	|	Сертификаты.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Шапка.Параметры.Заполнить(Выборка); 
		Шапка.АвтоМасштаб = Истина;
		ТабДок.Вывести(Шапка);
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
КонецПроцедуры

Функция СсылкаНаНовыйСертификат(Сумма) Экспорт
	
	НовыйОбъект 			= Справочники.Сертификаты.СоздатьЭлемент();
	НовыйОбъект.ДатаВыдачи 	= ТекущаяДата();
	НовыйОбъект.Сумма 		= Сумма;
	НовыйОбъект.Записать();
	ПроведениеНовыхСертификатов(НовыйОбъект.Ссылка, Сумма);
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Процедура ПроведениеНовыхСертификатов(СертификатОплаты, Сумма) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.Сертификаты.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Сертификат						= СертификатОплаты;
	МенеджерЗаписи.Период							= ТекущаяДата();
	МенеджерЗаписи.Погашен							= Ложь;
	МенеджерЗаписи.СуммаСертификата					= Сумма;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти