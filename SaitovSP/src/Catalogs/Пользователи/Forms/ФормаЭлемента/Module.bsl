#Область ОбработчикиСобытий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		//новый пользователь
		ВсеРоли = Метаданные.Роли;
		Для каждого Роль Из ВсеРоли Цикл
			СписокРолей.Добавить(Роль.Имя, Роль.Синоним);
		КонецЦикла;
		
	Иначе
		
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Объект.ИдентификаторПользователяИБ);
		
		Если ПользовательИБ <> Неопределено Тогда
			
			//это не новый пользователь
			ВсеРоли = Метаданные.Роли;
			Для каждого Роль Из ВсеРоли Цикл
				РольУстановлена = ПользовательИБ.Роли.Содержит(Роль);
				СписокРолей.Добавить(Роль.Имя, Роль.Синоним, РольУстановлена);
			КонецЦикла;

		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ДополнительныеСвойства.Вставить("СписокРолей", СписокРолей);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Объект.Наименование = СотрудникОбработкаВыбораНаСервере(Объект.Сотрудник);
	Объект.ПолноеИмя = Объект.Наименование;

КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	Объект.Наименование = СотрудникОбработкаВыбораНаСервере(Объект.Сотрудник);
	Объект.ПолноеИмя = Объект.Наименование;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура СписокРолейПометкаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СотрудникОбработкаВыбораНаСервере(СотрудникСсылка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.ИмяПользователя КАК ИмяПользователя
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СотрудникСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.ИмяПользователя;
	
КонецФункции

#КонецОбласти